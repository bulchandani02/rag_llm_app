    *RETRIEVAL
    *DMLIST
    *NO-ACTIVITY-LOG
    *SCHEMA-COMMENTS
 
     IDENTIFICATION DIVISION.
 
      PROGRAM-ID.         DEPTRPT.
 
      AUTHOR.           CA.                 
 
      DATE-WRITTEN.        APRIL 1995.
 
      REMARKS.           THIS PROGRAM DEMONSTRATES
                     CA IDMS DATABASE ACCESS USING
                     COBOL DML STATEMENTS. IT READS
                     DEPARTMENT ID NUMBERS AND RETRIEVES
                     RELATED RECORD OCCURRENCES,
                     PRINTING A REPORT THAT INCLUDES
                     DEPARTMENT, EMPLOYEE, JOB, AND
                     OFFICE INFORMATION.
    ***************************************************************
     ENVIRONMENT DIVISION.
     INPUT-OUTPUT SECTION.
     FILE-CONTROL.
       SELECT DEPT-FILE-IN      ASSIGN TO INFILE.
       SELECT DEPT-FILE-OUT     ASSIGN TO OUTFILE.
       SELECT ERR-FILE-OUT      ASSIGN TO ERRFILE.
    ***************************************************************
     IDMS-CONTROL SECTION.
 
     PROTOCOL.           MODE IS BATCH DEBUG
                    IDMS-RECORDS MANUAL.
       SKIP3
    ***************************************************************
     DATA DIVISION.
 
     SCHEMA SECTION.
 
     DB EMPSS01 WITHIN EMPSCHM.
 
    ***************************************************************
     FILE SECTION.
 
     FD DEPT-FILE-IN
       RECORD CONTAINS 80
       BLOCK CONTAINS 80 CHARACTERS
       RECORDING MODE IS F
       LABEL RECORDS ARE OMITTED.
 
     01 DEPT-REC-IN.
       02 DEPT-ID-IN       PIC 9(4).
       02 DEPT-IN-FILLER     PIC X(76).
 
     FD DEPT-FILE-OUT
       RECORD CONTAINS 133
       BLOCK CONTAINS 133 CHARACTERS
       RECORDING MODE IS F
       LABEL RECORDS ARE OMITTED.
 
     01 DEPT-REC-OUT.
       02 CC           PIC X.
       02 PRINT-LINE       PIC X(132).
 
     FD ERR-FILE-OUT
       RECORD CONTAINS 133
       BLOCK CONTAINS 133 CHARACTERS
       RECORDING MODE IS F
       LABEL RECORDS ARE OMITTED.
 
     01 ERR-REC-OUT.
       02 ERR-CC         PIC X.
       02 ERR-LINE        PIC X(132).
 
    ***************************************************************
     WORKING-STORAGE SECTION.
     01 EOF-SW       PIC X   VALUE 'N'.
       88 END-OF-FILE        VALUE 'Y'.
     01 LINE-COUNT    PIC 99   VALUE 0.
     01 ERR-LINE-COUNT  PIC 99   VALUE 0.
     01 LINE-MAX     PIC 99   VALUE 50.
    ***************************************************************
     01 DEPT-HEADER.
       05 FILLER    PIC X(30)  VALUE SPACES.
       05 FILLER    PIC X(13)  VALUE 'DEPARTMENT ID'.
       05 FILLER    PIC X(10)  VALUE SPACES.
       05 FILLER    PIC X(9)   VALUE 'DEPT NAME'.
       05 FILLER    PIC X(70)  VALUE SPACES.
     01 DEPT-DETAIL-LINE.
       05 FILLER    PIC X(33)  VALUE SPACES.
       05 DEPT-ID-OUT  PIC X(4).
       05 FILLER    PIC X(16)  VALUE SPACES.
       05 DEPT-NAME-OUT PIC X(45).
       05 FILLER    PIC X(34)  VALUE SPACES.
     01 EMP-HEADER.
       05 FILLER    PIC X(5)   VALUE SPACES.
       05 FILLER    PIC X(6)   VALUE 'EMP ID'.
       05 FILLER    PIC X(2)   VALUE SPACES.
       05 FILLER    PIC X(9)   VALUE 'LAST NAME'.
       05 FILLER    PIC X(8)   VALUE SPACES.
       05 FILLER    PIC X(10)  VALUE 'FIRST NAME'.
       05 FILLER    PIC X(3)   VALUE SPACES.
       05 FILLER    PIC X(10)  VALUE 'START DATE'.
       05 FILLER    PIC X(2)   VALUE SPACES.
       05 FILLER    PIC X(9)   VALUE 'JOB TITLE'.
       05 FILLER    PIC X(13)  VALUE SPACES.
       05 FILLER    PIC X(14)  VALUE 'OFFICE ADDRESS'.
       05 FILLER    PIC X(42)  VALUE SPACES.
     01 EMP-DETAIL-LINE.
       05 FILLER    PIC X(5)   VALUE SPACES.
       05 ID-OUT    PIC X(4).
       05 FILLER    PIC X(4)   VALUE SPACES.
       05 LAST-OUT   PIC X(15).
       05 FILLER    PIC X(2)   VALUE SPACES.
       05 FIRST-OUT   PIC X(10).
       05 FILLER    PIC X(3)   VALUE SPACES.
       05 SD-OUT.
         10 SD-MM   PIC XX.
         10 FILLER   PIC X    VALUE '/'.
         10 SD-DD   PIC XX.
         10 FILLER   PIC X    VALUE '/'.
         10 SD-YY   PIC XX.
       05 FILLER    PIC X(4)   VALUE SPACES.
       05 TITLE-OUT   PIC X(20).
       05 FILLER    PIC X(2)   VALUE SPACES.
       05 OFF-ADDRESS-OUT.
         10 STREET-OUT PIC X(20).
         10 FILLER   PIC XX    VALUE SPACES.
         10 CITY-OUT  PIC X(15).
         10 FILLER   PIC XX    VALUE SPACES.
         10 STATE-OUT PIC XX.
         10 FILLER   PIC XX    VALUE SPACES.
         10 ZIP-OUT  PIC X(5).
       05 FILLER    PIC X(8)   VALUE SPACES.
     01 ERR-HEADER-1.
       05 FILLER    PIC X(40)  VALUE SPACES.
       05 FILLER    PIC X(12)  VALUE 'ERROR REPORT'.
       05 FILLER    PIC X(80)  VALUE SPACES.
     01 ERR-HEADER-2.
       05 FILLER    PIC X(10)  VALUE SPACES.
       05 FILLER    PIC X(4)   VALUE '*** '.
       05 FILLER    PIC X(51)  VALUE
         'THIS REPORT LISTS EMPTY AND NONEXISTENT DEPARTMENTS'.
       05 FILLER    PIC X(4)   VALUE ' ***'.
       05 FILLER    PIC X(63)  VALUE SPACES.
     01 ERR-HEADER-3.
       05 FILLER    PIC X(20)  VALUE SPACES.
       05 FILLER    PIC X(7)   VALUE 'DEPT ID'.
       05 FILLER    PIC X(9)   VALUE SPACES.
       05 FILLER    PIC X(7)   VALUE 'MESSAGE'.
       05 FILLER    PIC X(89)  VALUE SPACES.
     01 ERR-DETAIL-LINE.
       05 FILLER    PIC X(20)  VALUE SPACES.
       05 ERR-ID-OUT  PIC X(4).
       05 FILLER    PIC X(12)  VALUE SPACES.
       05 ERR-MESS-OUT PIC X(15).
       05 FILLER    PIC X(79)  VALUE SPACES.
    ***************************************************************
     01 MESSAGES.
       05 NO-JOB-MESSAGE.
        10 FILLER     PIC X(20) VALUE 'NO JOB ASSIGNED'.
       05 NO-OFFICE-MESSAGE.
        10 FILLER     PIC X(20)
                  VALUE 'NO OFFICE ASSIGNED'.
       05 NO-DEPT-MESSAGE.
        10 FILLER     PIC X(15) VALUE 'DOES NOT EXIST'.
       05 NO-EMP-MESSAGE.
        10 FILLER     PIC X(15) VALUE 'IS EMPTY'.
       05 NO-INPUT-MESSAGE.
        10 FILLER     PIC XX   VALUE SPACES.
        10 FILLER     PIC X(11) VALUE '========>> '.
        10 FILLER     PIC X(8)  VALUE 'NO INPUT'.
        10 FILLER     PIC X(11) VALUE ' <<========'.
        10 FILLER     PIC X(100) VALUE SPACES.
 
     01 COPY IDMS SUBSCHEMA-CTRL.
 
     01 COPY IDMS SUBSCHEMA-SSNAME.
 
     01 COPY IDMS SUBSCHEMA-RECNAMES.
 
     01 COPY IDMS SUBSCHEMA-SETNAMES.
 
     01 COPY IDMS RECORD EMPLOYEE.
 
     01 COPY IDMS RECORD DEPARTMENT.
 
     01 COPY IDMS RECORD JOB.
 
     01 COPY IDMS RECORD EMPOSITION.
 
     01 COPY IDMS RECORD OFFICE.
       EJECT
     PROCEDURE DIVISION.
 
    *  *********************************************************
    *  * PROCEDURE DIVISION GENERAL STRATEGY:         *
    *  *   1) READ DEPT-ID-IN, WHICH CONTAINS THE      *
    *  *     DEPT-ID NUMBER                *
    *  *   2) ACCESS THE DATABASE USING THE DEPT-ID NUMBER *
    *  *     WITH AN OBTAIN CALC ON THE DEPARTMENT RECORD *
    *  *   3) ACCESS ALL EMPLOYEES IN THE DEPT-EMPLOYEE SET *
    *  *     AND RETRIEVE RELATED JOB AND OFFICE DATA   *
    *  *   4) PRINT A REPORT FOR EACH DEPARTMENT      *
    *  *   5) PRINT AN ERROR REPORT FOR EMPTY DEPARTMENTS  *
    *  *     AND NONEXISTENT DEPARTMENTS (NO MATCHING   *
    *  *     DEPT-ID)                   *
    *  *********************************************************
 
     MAIN-LINE.
       PERFORM INIT-FILES.
       IF END-OF-FILE
        PERFORM EMPTY-INPUT-PROCESSING
       ELSE
        PERFORM INIT-BIND-READY
        PERFORM U220-ERR-HEADER
        PERFORM DEPT-PROCESSING THRU DEPT-PROCESSING-EXIT
               UNTIL END-OF-FILE.
       PERFORM END-PROCESSING.
       GOBACK.
 
     INIT-BIND-READY.
    ***************************************************************
    * THE BIND STATEMENTS ARE PERFORMED INDIVIDUALLY (RATHER   *
    * THAN BY USING A COPY IDMS SUBSCHEMA-BINDS) IN ORDER TO   *
    * CHECK EACH ERROR-STATUS BY PERFORMING THE IDMS-STATUS    *
    * ROUTINE.                          *
    ***************************************************************
       MOVE 'DEPTRPT' TO PROGRAM-NAME.
       BIND RUN-UNIT.
       PERFORM IDMS-STATUS.
       BIND EMPLOYEE.
       PERFORM IDMS-STATUS.
       BIND DEPARTMENT.
       PERFORM IDMS-STATUS.
       BIND JOB.
       PERFORM IDMS-STATUS.
       BIND EMPOSITION.
       PERFORM IDMS-STATUS.
       BIND OFFICE.
       PERFORM IDMS-STATUS.
       READY.
       PERFORM IDMS-STATUS.
 
     INIT-FILES.
       OPEN INPUT DEPT-FILE-IN.
       OPEN OUTPUT DEPT-FILE-OUT.
       OPEN OUTPUT ERR-FILE-OUT.
       MOVE SPACES TO PRINT-LINE.
       MOVE SPACES TO ERR-LINE.
       READ DEPT-FILE-IN AT END MOVE 'Y' TO EOF-SW.
 
     EMPTY-INPUT-PROCESSING.
       MOVE NO-INPUT-MESSAGE TO PRINT-LINE.
       MOVE '1' TO CC.
       PERFORM U000-WRITE-LINE.
 
    ***************************************************************
    * THIS PARAGRAPH ACCESSES THE DATABASE USING THE DEPT-ID-0415 *
    * CALCKEY VALUE.                       *
    ***************************************************************
     DEPT-PROCESSING.
       MOVE DEPT-ID-IN TO DEPT-ID-0410.
       OBTAIN CALC DEPARTMENT.
       IF DB-REC-NOT-FOUND THEN
         PERFORM NO-DEPT-PROCESSING
       ELSE
         PERFORM IDMS-STATUS
         IF DEPT-EMPLOYEE IS NOT EMPTY THEN
           PERFORM U020-VALID-HEADER
           MOVE DEPT-ID-0410 TO DEPT-ID-OUT
           MOVE DEPT-NAME-0410 TO DEPT-NAME-OUT
           MOVE DEPT-DETAIL-LINE TO PRINT-LINE
           PERFORM U000-WRITE-LINE
           PERFORM U030-EMP-HEADERS
           PERFORM SET-WALK THRU SET-WALK-EXIT
                 UNTIL DB-END-OF-SET
         ELSE
           PERFORM EMPTY-SET.
       READ DEPT-FILE-IN AT END MOVE 'Y' TO EOF-SW.
     DEPT-PROCESSING-EXIT.
       EXIT.
 
    ***************************************************************
    * THIS PARAGRAPH RETRIEVES EMPLOYEE, JOB, AND OFFICE DATA   *
    * FOR EACH EMPLOYEE IN THE DEPT-EMPLOYEE SET.         *
    ***************************************************************
     SET-WALK.
       OBTAIN NEXT EMPLOYEE WITHIN DEPT-EMPLOYEE.
       IF DB-END-OF-SET
        GO TO SET-WALK-EXIT
       ELSE
        PERFORM IDMS-STATUS.
       MOVE EMP-ID-0415 TO ID-OUT.
       MOVE EMP-LAST-NAME-0415 TO LAST-OUT.
       MOVE EMP-FIRST-NAME-0415 TO FIRST-OUT.
       MOVE START-YEAR-0415 TO SD-YY.
       MOVE START-MONTH-0415 TO SD-MM.
       MOVE START-DAY-0415 TO SD-DD.
       IF EMP-EMPOSITION IS EMPTY
         MOVE NO-JOB-MESSAGE TO TITLE-OUT
       ELSE
         FIND FIRST WITHIN EMP-EMPOSITION
         PERFORM IDMS-STATUS
         IF NOT JOB-EMPOSITION MEMBER
           MOVE NO-JOB-MESSAGE TO TITLE-OUT
         ELSE
           OBTAIN OWNER WITHIN JOB-EMPOSITION
           PERFORM IDMS-STATUS
           MOVE TITLE-0440 TO TITLE-OUT.
       IF OFFICE-EMPLOYEE IS EMPTY
         MOVE NO-OFFICE-MESSAGE TO STREET-OUT
         MOVE SPACES TO CITY-OUT
         MOVE SPACES TO STATE-OUT
         MOVE SPACES TO ZIP-OUT
       ELSE
         OBTAIN OWNER WITHIN OFFICE-EMPLOYEE
         PERFORM IDMS-STATUS
         MOVE OFFICE-STREET-0450 TO STREET-OUT
         MOVE OFFICE-CITY-0450 TO CITY-OUT
         MOVE OFFICE-STATE-0450 TO STATE-OUT
         MOVE OFFICE-ZIP-FIRST-FIVE-0450 TO ZIP-OUT
         MOVE EMP-DETAIL-LINE TO PRINT-LINE.
       PERFORM U000-WRITE-LINE.
     SET-WALK-EXIT.
       EXIT.
 
     END-PROCESSING.
       FINISH.
       PERFORM IDMS-STATUS.
       CLOSE DEPT-FILE-OUT.
       CLOSE ERR-FILE-OUT.
       CLOSE DEPT-FILE-IN.
 
     EMPTY-SET.
       MOVE SPACES TO ERR-LINE.
       MOVE DEPT-ID-0410 TO ERR-ID-OUT.
       MOVE NO-EMP-MESSAGE TO ERR-MESS-OUT.
       MOVE ERR-DETAIL-LINE TO ERR-LINE.
       PERFORM U200-WRITE-ERR-LINE.
 
     NO-DEPT-PROCESSING.
       MOVE DEPT-ID-IN TO ERR-ID-OUT.
       MOVE NO-DEPT-MESSAGE TO ERR-MESS-OUT.
       MOVE ERR-DETAIL-LINE TO ERR-LINE.
       PERFORM U200-WRITE-ERR-LINE.
 
     U000-WRITE-LINE.
       WRITE DEPT-REC-OUT AFTER POSITIONING CC.
       IF CC = '1' THEN MOVE 0 TO LINE-COUNT
        ELSE IF CC = ' ' THEN ADD 1 TO LINE-COUNT
          ELSE IF CC = '0' THEN ADD 2 TO LINE-COUNT.
       IF LINE-COUNT > LINE-MAX
             THEN PERFORM U010-NEW-PAGE-ROUTINE.
     U010-NEW-PAGE-ROUTINE.
       PERFORM U020-VALID-HEADER.
       MOVE DEPT-DETAIL-LINE TO PRINT-LINE.
       PERFORM U000-WRITE-LINE.
       PERFORM U030-EMP-HEADERS.
     U020-VALID-HEADER.
       MOVE DEPT-HEADER TO PRINT-LINE.
       MOVE '1' TO CC.
       PERFORM U000-WRITE-LINE
       MOVE ' ' TO CC.
     U030-EMP-HEADERS.
       MOVE '0' TO CC.
       MOVE EMP-HEADER TO PRINT-LINE.
       PERFORM U000-WRITE-LINE.
       MOVE SPACES TO PRINT-LINE.
       MOVE ' ' TO CC.
       PERFORM U000-WRITE-LINE.
 
     U200-WRITE-ERR-LINE.
       WRITE ERR-REC-OUT AFTER POSITIONING ERR-CC.
       IF ERR-CC = '1' THEN MOVE 0 TO ERR-LINE-COUNT
        ELSE IF ERR-CC = ' ' THEN ADD 1 TO ERR-LINE-COUNT
         ELSE IF ERR-CC = '0' THEN ADD 2 TO ERR-LINE-COUNT.
       IF ERR-LINE-COUNT > LINE-MAX THEN
                PERFORM U220-ERR-HEADER.
     U220-ERR-HEADER.
       MOVE ERR-HEADER-1 TO ERR-LINE.
       MOVE '1' TO ERR-CC.
       PERFORM U200-WRITE-ERR-LINE
       MOVE '0' TO ERR-CC.
       MOVE ERR-HEADER-2 TO ERR-LINE.
       PERFORM U200-WRITE-ERR-LINE.
       MOVE ERR-HEADER-3 TO ERR-LINE.
       PERFORM U200-WRITE-ERR-LINE.
       MOVE SPACES TO ERR-LINE.
       MOVE ' ' TO ERR-CC.
       PERFORM U200-WRITE-ERR-LINE.
     IDMS-ABORT.
       EXIT.
     IDMS-ABORT-EXIT.
       COPY IDMS IDMS-STATUS.